<process> ::= start <endableFlow>

<numOfTasks> ::= GE_RANGE:20

<numOfGateways> ::= GE_RANGE:20

<endableFlow> ::= task(<numOfTasks>) <endableFlow>|
                  <parallel> <endableFlow>|
                  <exclusive> <endableFlow>|
                  exsplit <endableFlow> <nextBranchEndable>|
                  <loop> <endableFlow>|
                  do <flow> repeat_after <flow> back <endableFlow>|
                  end

<flow> ::= task(<numOfTasks>) <flow>|
           task(<numOfTasks>)|
           <parallel> <flow>|
           <parallel>|
           <loop> <flow>|
           <loop>|
           <exclusive> <flow>|
           <exclusive>|
           do <flow> repeat_after <flow> back|
           do <flow> repeat_after <flow> back <flow>
           

<parallel> ::= par <flow> <nextBranch>

<exclusive> ::= ex <flow> <nextBranch>

<loop> ::= loop <flow> back

<nextBranch> ::= next <flow> <nextBranch>|
                 next <nextBranch>|
                 next <flow> back|
                 next back

<nextBranchEndable> ::= next <endableGotoFlow> back|
                        next <endableGotoFlow> <nextBranchEndable>

<identifier> ::= task<numOfTasks>|
                 gat<numOfGateways>

<endableGotoFlow> ::= task(<numOfTasks>) <endableGotoFlow>|
                      <parallel> <endableGotoFlow>|
                      <exclusive> <endableGotoFlow>|
                      exsplit <endableGotoFlow> <nextBranchEndable>|
                      <loop> <endableGotoFlow>|
                      do <flow> repeat_after <flow> back <endableGotoFlow>|
                      end|
                      goto(<identifier>)